git = commitLine <newline>
      treeLine <newline>
      (* The root of the repo won't have a parent, so this is optional *)
      [ parentLine <newline> ]
      authorLine <newline>
      committerLine <newline>
      <newline>
      msgTitle
      [ msgBody <newline> ]

commitLine = <"commit"> <sp> sha
treeLine = <"tree"> <sp> sha
parentLine = <["parent" sp sha newline]> (* In case of a merge commit, disregard
                                            the first parent.  Is this technically right? *)
             <"parent"> <sp> sha
authorLine = <"author"> <sp> name <bracket> emailAddr <bracket> <sp> time
committerLine = <"committer"> <sp> name <bracket> emailAddr <bracket> <sp> time

msgTitle = <indent> any*
msgBody = ( newline <indent> any* )*

emailAddr = #"[^>]+"
bracket = #"[<>]"
name = #"[^<]+" (* trim this *)
time = epoch <sp> offset
epoch = #"[0-9]+"
offset = ( '-' | '+' ) #"[0-9]{4}"
date = #"\d{4}-\d{2}-\d{2}T\d{2}:\d{2}:\d{2}.\d{2}:\d{2}"
sha = #"[a-f0-9]+"
sp = #"[ ]+"
oneSp = ' '
indent = oneSp oneSp oneSp oneSp
newline = '\n'
any = #"."
